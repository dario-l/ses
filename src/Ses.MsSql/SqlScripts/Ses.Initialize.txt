CREATE TABLE [Streams] (
    [InternalId] BIGINT NOT NULL IDENTITY(1,1),
    [StreamId] uniqueidentifier NOT NULL,
    [Version] int NOT NULL,
    [CommitId] uniqueidentifier NOT NULL,
    [ContractName] nvarchar(225) NOT NULL,
    [CreatedAtUtc] datetime NOT NULL,
    [Payload] varbinary(max) NOT NULL,
    [EventId] BIGINT NULL
    CONSTRAINT [PK_Streams] PRIMARY KEY CLUSTERED ([StreamId],[Version])
);

CREATE UNIQUE NONCLUSTERED INDEX [IX_Streams_CommitId_Version] ON [dbo].[Streams] (
    [CommitId] ASC,
    [Version] ASC
);

CREATE NONCLUSTERED INDEX [IX_Streams_EventId_InternalId] ON [dbo].[Streams] (
    [EventId] ASC,
    [InternalId] ASC
)
INCLUDE (
    [StreamId],
    [Version],
    [CommitId],
    [ContractName],
    [Payload],
    [CreatedAtUtc]
)
WHERE ([EventId] IS NOT NULL);

CREATE NONCLUSTERED INDEX [IX_Streams_InternalId] ON [dbo].[Streams] ([InternalId])
INCLUDE ([StreamId],[Version],[EventId]);

CREATE TABLE [StreamsMetadata] (
    [StreamId] uniqueidentifier NOT NULL,
    [CommitId] uniqueidentifier NOT NULL,
    [Payload] varbinary(max) NOT NULL,
    CONSTRAINT [PK_StreamsMetadata] PRIMARY KEY CLUSTERED ([StreamId],[CommitId])
);

CREATE TABLE [StreamsSnapshots] (
    [StreamId] uniqueidentifier NOT NULL,
    [Version] int NOT NULL,
    [LastStreamVersion] int NOT NULL,
    [ContractName] nvarchar(225) NOT NULL,
    [GeneratedAtUtc] datetime NOT NULL,
    [Payload] varbinary(max) NOT NULL,
    CONSTRAINT [PK_StreamsSnapshots] PRIMARY KEY CLUSTERED ([StreamId],[Version])
);

CREATE TABLE [dbo].[StreamsSubscriptions] (
    [Id] [int] IDENTITY(1,1) NOT NULL,
    [Name] [nvarchar](50) NOT NULL,
    CONSTRAINT [PK_StreamsSubscriptions_1] PRIMARY KEY CLUSTERED ([Id] ASC)
);

CREATE TABLE [dbo].[StreamsSubscriptionContracts](
    [StreamsSubscriptionId] [int] NOT NULL,
    [EventContractName] [nvarchar](225) NOT NULL,
    CONSTRAINT [PK_StreamsSubscriptionContracts_1] PRIMARY KEY CLUSTERED
    (
        [EventContractName] ASC,
        [StreamsSubscriptionId] ASC
    )
);
GO

CREATE PROCEDURE [dbo].[SesSelectEvents]
(
    @StreamId UNIQUEIDENTIFIER,
    @FromVersion INT,
    @PessimisticLock BIT
)
AS
BEGIN
    SET NOCOUNT ON;
    IF(@PessimisticLock = 1) BEGIN
        DECLARE @LockedStreamId uniqueidentifier
        SELECT TOP 1 @LockedStreamId = [StreamId] FROM [Streams] WITH (UPDLOCK,ROWLOCK) WHERE [StreamId] = @StreamId AND [Version] = 0
        IF @LockedStreamId IS NULL BEGIN
            RAISERROR('StreamIsNotLockable', 16, 1);
            RETURN;
        END
    END

    DECLARE @SPayload VARBINARY(MAX), @SContractName NVARCHAR(255), @SVersion int

    SELECT
        @SContractName = [ContractName],
        @SVersion = [Version],
        @SPayload = [Payload]
    FROM
        [StreamsSnapshots] WITH(READPAST,ROWLOCK)
    WHERE
        [StreamId] = @StreamId
        AND [Version] > 0
    
    SELECT @SContractName, @SVersion, @SPayload

    IF @SVersion IS NOT NULL BEGIN
        SET @FromVersion = @SVersion + 1
    END

    SELECT
        [ContractName],
        [Version],
        [Payload]
    FROM
        [Streams] WITH(READPAST,ROWLOCK)
    WHERE
        [StreamId] = @StreamId
        AND [Version] >= @FromVersion
    ORDER BY
        [Version] ASC
END;
GO

CREATE PROCEDURE [dbo].[SesInsertEventsNoStream]
(
    @StreamId UNIQUEIDENTIFIER,
    @CommitId UNIQUEIDENTIFIER,
    @CreatedAtUtc DATETIME,
    @MetadataPayload VARBINARY(MAX),
    @IsLockable BIT,
    @EventContractName NVARCHAR(250),
    @EventVersion INT,
    @EventPayload VARBINARY(MAX)
)
AS
BEGIN
    SET NOCOUNT ON;
    IF @EventVersion = 1 AND EXISTS(SELECT TOP 1 1 FROM [Streams] WHERE [StreamId] = @StreamId) BEGIN
        RAISERROR('WrongExpectedVersion', 16, 1);
        RETURN;
    END

    IF @EventVersion = 1 BEGIN
        INSERT INTO [StreamsSnapshots]([StreamId], [Version], [LastStreamVersion], [ContractName], [GeneratedAtUtc], [Payload])
        VALUES(@StreamId, 0, 0, 'Init', @CreatedAtUtc, 0);

        IF @IsLockable = 1 BEGIN
            INSERT INTO [Streams]([StreamId], [CommitId], [Version], [ContractName], [Payload], [CreatedAtUtc])
            VALUES(@StreamId, @CommitId, 0, 'Lockable', 0, @CreatedAtUtc);
        END
    END

    INSERT INTO [Streams]([StreamId], [CommitId], [Version], [ContractName], [Payload], [CreatedAtUtc])
    VALUES(@StreamId, @CommitId, @EventVersion, @EventContractName, @EventPayload, @CreatedAtUtc);

    IF(@MetadataPayload IS NOT NULL AND LEN(@MetadataPayload) > 0) BEGIN
        INSERT INTO [StreamsMetadata]([StreamId], [CommitId], [Payload])
        VALUES(@StreamId, @CommitId, @MetadataPayload);
    END
END;
GO

CREATE PROCEDURE [dbo].[SesInsertEventsAny]
(
    @StreamId UNIQUEIDENTIFIER,
    @CommitId UNIQUEIDENTIFIER,
    @CreatedAtUtc DATETIME,
    @MetadataPayload VARBINARY(MAX),
    @IsLockable BIT,
    @EventContractName NVARCHAR(250),
    @EventVersion INT,
    @EventPayload VARBINARY(MAX)
)
AS
BEGIN
    SET NOCOUNT ON;
    DECLARE @IsNew BIT = 0
    IF NOT EXISTS(SELECT TOP 1 1 FROM [Streams] WHERE [StreamId] = @StreamId) BEGIN
        SET @IsNew = 1
    END

    IF(@IsNew = 1 AND @IsLockable = 1) BEGIN
        INSERT INTO [Streams]([StreamId], [CommitId], [Version], [ContractName], [CreatedAtUtc], [Payload])
        VALUES(@StreamId, @CommitId, 0, 'Lockable', @CreatedAtUtc, 0);
    END

    INSERT INTO [Streams]([StreamId], [CommitId], [Version], [ContractName], [Payload], [CreatedAtUtc])
    VALUES(@StreamId, @CommitId, @EventVersion, @EventContractName, @EventPayload, @CreatedAtUtc);

    IF(@MetadataPayload IS NOT NULL AND LEN(@MetadataPayload) > 0) BEGIN
        INSERT INTO [StreamsMetadata]([StreamId], [CommitId], [Payload])
        VALUES(@StreamId, @CommitId, @MetadataPayload);
    END

    IF(@IsNew = 1 AND @IsLockable = 1) BEGIN
        INSERT INTO [StreamsSnapshots]([StreamId], [Version], [LastStreamVersion], [ContractName], [GeneratedAtUtc], [Payload])
        VALUES(@StreamId, 0, 0, 'Init', @CreatedAtUtc, 0);
    END
END;
GO

CREATE PROCEDURE [dbo].[SesInsertEventsExpectedVersion]
(
    @StreamId UNIQUEIDENTIFIER,
    @CommitId UNIQUEIDENTIFIER,
    @CreatedAtUtc DATETIME,
    @MetadataPayload VARBINARY(MAX),
    @ExpectedVersion INT,
    @EventContractName NVARCHAR(250),
    @EventVersion INT,
    @EventPayload VARBINARY(MAX)
)
AS
BEGIN
    SET NOCOUNT ON;
    IF(SELECT TOP 1 1 FROM [Streams] WHERE [StreamId] = @StreamId AND (SELECT TOP 1 [Version] FROM [Streams] WHERE [StreamId] = @StreamId ORDER BY [Version] DESC) = @ExpectedVersion) <> 1 BEGIN
        RAISERROR('WrongExpectedVersion', 16, 1);
        RETURN;
    END

    INSERT INTO [Streams]([StreamId], [CommitId], [Version], [ContractName], [Payload], [CreatedAtUtc])
    VALUES(@StreamId, @CommitId, @EventVersion, @EventContractName, @EventPayload, @CreatedAtUtc);

    IF(@MetadataPayload IS NOT NULL AND LEN(@MetadataPayload) > 0) BEGIN
        INSERT INTO [StreamsMetadata]([StreamId], [CommitId], [Payload])
        VALUES(@StreamId, @CommitId, @MetadataPayload);
    END
END;
GO

CREATE PROCEDURE SesSelectTimelineEvents
(
    @Limit INT,
    @SequenceId BIGINT
)
AS
BEGIN
    SET NOCOUNT ON;
    SELECT TOP(@Limit)
        s.[ContractName] AS [EventContractName],
        s.[Payload] AS [EventPayload],
        sm.[Payload] AS [MetaPayload],
        s.[StreamId],
        s.[CommitId],
        s.[CreatedAtUtc],
        s.[EventId],
        s.[Version]
    FROM
        [Streams] s WITH(READPAST,ROWLOCK)
        LEFT JOIN [StreamsMetadata] sm WITH(NOLOCK) ON sm.StreamId = s.StreamId AND sm.CommitId = s.CommitId
    WHERE
        s.[EventId] > @SequenceId
    ORDER BY
        s.[EventId] ASC
END;
GO

CREATE PROCEDURE SesSelectTimelineSubscriptionEvents
(
    @Limit INT,
    @SequenceId BIGINT,
    @SubscriptionId INT
)
AS
BEGIN
    SET NOCOUNT ON;
    SELECT TOP(@Limit)
        s.[ContractName] AS [EventContractName],
        s.[Payload] AS [EventPayload],
        sm.[Payload] AS [MetaPayload],
        s.[StreamId],
        s.[CommitId],
        s.[CreatedAtUtc],
        s.[EventId],
        s.[Version]
    FROM
        [Streams] s WITH(READPAST,ROWLOCK)
        INNER JOIN [StreamsSubscriptionContracts] ssc WITH(NOLOCK) ON ssc.EventContractName = s.ContractName
        LEFT JOIN [StreamsMetadata] sm WITH(NOLOCK) ON sm.StreamId = s.StreamId AND sm.CommitId = s.CommitId
    WHERE
        s.[EventId] > @SequenceId
        AND ssc.StreamsSubscriptionId = @SubscriptionId
    ORDER BY
        s.[EventId] ASC
END;
GO

CREATE PROCEDURE SesDeleteStreamAny
(
    @StreamId UNIQUEIDENTIFIER
)
AS BEGIN
    SET NOCOUNT ON;
    DELETE FROM [StreamsSnapshots] WHERE [StreamId] = @StreamId
    DELETE FROM [StreamsMetadata] WHERE [StreamId] = @StreamId
    DELETE FROM [Streams] WHERE [StreamId] = @StreamId
END;
GO

CREATE PROCEDURE SesDeleteStreamExpectedVersion
(
    @StreamId UNIQUEIDENTIFIER,
    @ExpectedVersion INT
)
AS BEGIN
    SET NOCOUNT ON;
    
    DELETE FROM [StreamsSnapshots]
    WHERE
        [StreamId] = @StreamId
        AND (
            SELECT
                COUNT([Version])
            FROM
                [Streams]
            WHERE
                [Version] >= @ExpectedVersion
                AND [StreamId] = @StreamId
        ) = 1
    
    IF @@ROWCOUNT = 0 BEGIN
        RAISERROR('WrongExpectedVersion', 16, 1);
        RETURN;
    END
    DELETE FROM [StreamsMetadata] WHERE [StreamId] = @StreamId
    DELETE FROM [Streams] WHERE [StreamId] = @StreamId
END;
GO

CREATE PROCEDURE SesUpdateSnapshot
(
    @StreamId UNIQUEIDENTIFIER,
    @GeneratedAtUtc DATETIME,
    @Version INT,
    @ContractName NVARCHAR(225),
    @Payload VARBINARY(MAX)
)
AS BEGIN
    SET NOCOUNT ON;
    
    UPDATE [StreamsSnapshots] SET
        GeneratedAtUtc = @GeneratedAtUtc,
        Version = @Version,
        Payload = @Payload,
        ContractName = @ContractName
    WHERE
        [StreamId] = @StreamId
    
    IF @@ROWCOUNT = 0 BEGIN
        RAISERROR('WrongExpectedVersion', 16, 1);
        RETURN;
    END
END;
GO

CREATE PROCEDURE SesLinearize
(
    @Limit INT
)
AS
BEGIN
    SET NOCOUNT ON;
    DECLARE @id BIGINT, @Version INT, @StreamId UNIQUEIDENTIFIER, @EventId BIGINT

    SELECT @EventId = IsNull(MAX(EventId), 0) FROM Streams WITH (READPAST,ROWLOCK)

    DECLARE cur CURSOR FAST_FORWARD FOR
        SELECT TOP(@Limit) InternalId, Version, StreamId FROM Streams WITH (READPAST,ROWLOCK) WHERE EventId IS NULL ORDER BY InternalId ASC
    OPEN cur
    FETCH NEXT FROM cur INTO @id, @Version, @StreamId
    WHILE @@FETCH_STATUS = 0 BEGIN
        SET @EventId = @EventId + 1
        UPDATE Streams SET EventId = @EventId WHERE InternalId = @id
        UPDATE StreamsSnapshots SET [LastStreamVersion] = @Version WHERE [StreamId] = @StreamId
    FETCH NEXT FROM cur INTO @id, @Version, @StreamId END
    CLOSE cur
    DEALLOCATE cur
END;
GO